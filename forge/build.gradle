apply from: "https://maven.itsmeow.dev/scripts/architectury-forge.gradle"

configurations {
    shadowModImplementation
    modImplementation.extendsFrom shadowModImplementation
}

repositories {
    maven {
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {
    modCompileOnly "top.theillusivec4.curios:curios-forge:${rootProject.curios_version}:api"
    modRuntimeOnly "top.theillusivec4.curios:curios-forge:${rootProject.curios_version}"
    shadowModImplementation("dev.itsmeow.imdlib:imdlib-forge:${rootProject.imdlib_version}") {
        transitive = false
    }
}

loom {
    launches {
        all {
            arg "-mixin.config", "mixin.imdlib.json"
            /*property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"*/
        }
    }
    forge {
        mixinConfig "mixin.imdlib-betteranimalsplus.json", "betteranimalsplus.mixins.json", "betteranimalsplus-forge.mixins.json"
    }
}


shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.shadowModImplementation]
    relocate 'dev.itsmeow.imdlib', "${rootProject.maven_group}.imdlib"
    classifier "dev-shadow"
    exclude "mixin.imdlib-betteranimalsplus.json"
    exclude "architectury-common.accessWidener"
}

remapJar {
    classifier "remap"
}

task fixJar(type: Jar) {
    dependsOn remapJar
    from remapJar.archiveFile.map { zipTree(it) }
    manifest {
        from {
            zipTree(remapJar.archiveFile).find {
                it.name == "MANIFEST.MF"
            }
        }
    }
    delete("mixin.imdlib-betteranimalsplus.json")
    rename("mixin.imdlib.json", "mixin.imdlib-betteranimalsplus.json")
    rename("imdlib-common-refmap.json", "imdlib-betteranimalsplus-common-refmap.json")
    filesMatching("mixin.imdlib.json") {
        filter {
            line ->
                {
                    line = line.replaceAll('dev.itsmeow.imdlib', 'dev.itsmeow.betteranimalsplus.imdlib')
                    line.replaceAll('imdlib-common-refmap.json', 'imdlib-betteranimalsplus-common-refmap.json')
                }
        }
    }
    filesMatching("imdlib-common-refmap.json") {
        filter {
            line -> line.replaceAll('dev/itsmeow/imdlib/mixin/', 'dev/itsmeow/betteranimalsplus/imdlib/mixin/')
        }
    }
    classifier "forge"
}

sourcesJar.dependsOn fixJar